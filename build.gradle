plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.21.2'
}

def projectGroup = 'com.degoos'
def projectId = "JAMS"
def projectMainClass = "net.jamsimulator.jams.Jams"

def javafxVersion = '14.0.2.1'
def javafxDependencies = ['javafx-base', 'javafx-controls', 'javafx-media', 'javafx-graphics', 'javafx-fxml']
def bundleLibs = false
def winConsole = false;

group projectGroup
version '0.4-ALPHA'

repositories {
    jcenter()
    maven {
        url "https://jitpack.io"
    }
}

configurations {
    jarLib
}

javafx {
    version = javafxVersion
    modules = ['javafx.base', 'javafx.controls', 'javafx.media', 'javafx.graphics', 'javafx.fxml']
}

application {
    mainModule.set("JAMS.main")
    mainClass.set(projectMainClass)
}

dependencies {
    implementation group: 'com.github.goxr3plus', name: 'FX-BorderlessScene', version: '4.4.0'
    implementation group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.5'
    implementation group: 'org.json', name: 'json', version: '20200518'

    javafxDependencies.each {
        jarLib group: 'org.openjfx', name: it, version: javafxVersion, classifier: 'win'
        jarLib group: 'org.openjfx', name: it, version: javafxVersion, classifier: 'mac'
        jarLib group: 'org.openjfx', name: it, version: javafxVersion, classifier: 'linux'
    }

    jarLib group: 'com.github.goxr3plus', name: 'FX-BorderlessScene', version: '4.4.0'
    jarLib group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.5'
    jarLib group: 'org.json', name: 'json', version: '20200518'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}

compileJava {
    options.encoding = 'UTF-8'
    options.incremental = true
}

processResources {
    from('src/main/resources') {
        include '*.json'
        filter { String line -> line.replace("\${VERSION}", project.version) }
    }
}

jar {
    manifest {
        attributes "Main-Class": projectMainClass
        attributes "Specification-Title": projectId
        attributes "Specification-Version": archiveVersion
        attributes "Specification-Vendor": projectGroup
        attributes "Implementation-Title": projectId
        attributes "Implementation-Version": archiveVersion
        attributes "Implementation-Vendor": projectGroup
    }

    doFirst {
        if (bundleLibs) {
            from {
                configurations.jarLib.collect { it.isDirectory() ? it : zipTree(it) }
            }
        }
    }
}

task bundle {
    dependsOn clean
    group = "build"

    doLast {
        bundleLibs = true
    }

    finalizedBy build
}

jlink {
    options.addAll(['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'])
    launcher {
        name = 'JAMS'
        jvmArgs = ['-Dlogback.configurationFile=./logback.xml']
    }
    addExtraDependencies("javafx")
    jpackage {
        def ver = normalizeAppVersion(version as String)

        //Splash is bugged in windows.
        //jvmArgs = ['-splash:$APPDIR/logo.png']

        imageOptions = ["--verbose", "--app-version", ver]
        installerOptions = ['--app-version', ver,
                            '--vendor', 'jamsimulator.net',
                            '--file-associations', 'src/main/resources/associations.properties']
        imageOptions = ['--icon', 'src/main/resources/gui/icon/logo.ico']
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            if(winConsole) {
                imageOptions += ['--win-console']
            }
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu']
        } else if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerOptions += ['--linux-menu-group', 'programming']
            installerType = "deb"
        }
    }
}

static String normalizeAppVersion(final String appVersion) {
    if (org.gradle.internal.os.OperatingSystem.current().linux) {
        // Replace '-' with '.' for rpmbuild
        return appVersion.replace('-', '.')
    } else if (org.gradle.internal.os.OperatingSystem.current().windows) {
        // This is a hack attempt to assure the version conforms to MSI productVersion string rules
        // See https://docs.microsoft.com/en-us/windows/win32/msi/productversion
        // For now, we'll just remove '-SNAPSHOT' if present.
        return appVersion.replaceAll('-SNAPSHOT$', '').replaceAll('-ALPHA$', '')
    } else {
        return appVersion
    }
}

tasks.jpackageImage.doLast {
    copy {
        from "src/main/resources/gui/icon"
        include "logo.png"
        into "build/jpackage/$project.name/app"
    }
}